/*
** brei.prg was written by Joakim Westerlund, Department of Economics, Lund University.
** Contact info:  joakim.westerlund@nek.lu.se
**
** The code can be used freely as long as proper reference is given. No performance
** guarantee is made. Bug reports are welcome.
*/
proc (2) = brei_panel(y, x, mod);
    local i, b1, b2, mu, var, k, n;
    
    i  = 1;
    b1 = 0;
    n = cols(y);
    k = cols(x)/n;
    
    do until i > n;
        
        b1 = b1 + brei(y[., i], x[., 1+(i-1)*k:i*k], mod);
        
        i = i + 1;
        
    endo;
    
    { mu, var } = breimom(mod, k);
    b1 = sqrt(n)*(b1/n - mu)/sqrt(var);
    
    { mu, var } = pbreimom(mod, k);
    b2  = sqrt(n)*(pbrei(y, x, mod) - mu)/sqrt(var);
    
    format/m1/rd 8,3;
    print " ";
    print " vr_g = ";;
    b1;
    print " vr_p = ";;
    b2;
    
    retp(b1, b2);
endp;

proc (1) = pbrei(y, x, mod);
    local i, t, n, k, u, br, v;
    
    t = rows(y);
    n = cols(y);
    k = cols(x)/n;
    
    i = 1;
    u = zeros(t, 1);
    do until i > n;
        
        if mod == 0;
            v = x[., 1+(i-1)*k:i*k];
        elseif mod == 1;
            v = ones(t, 1)~x[., 1+(i-1)*k:i*k];
        elseif mod == 2;
            v = ones(t, 1)~seqa(1, 1, t)~x[., 1+(i-1)*k:i*k];
        endif;
        
        u = u~(eye(t) - v*inv(v'v)*v')*y[., i];
        
        i = i + 1;
    endo;
    
    u = vec(u[.,2:cols(u)]);
    br = (t^(-1)*sumc(abs(cumsumc(u)).^2))/(sumc(u^2));
    
    retp(t^(-1)*br);
endp;



proc(2) = pbreimom(mod, k);
    local mu, var;
    
    mu = {  0.04417266	0.02952784	0.01913412	0.01290706	0.00913085	0.00670546	0.00492034 ,
        0.01044646	0.00817416	0.00643255	0.00519797	0.00405747	0.00331805	0.00273211 };
    
    var = { 0.00161609	0.00142304	0.00104175	0.00051325	0.00027744	0.00013565	0.00006230 ,
        0.00004747	0.00004034	0.00002876	0.00002354	0.00001263	0.00000895	0.00000561 };
    
    retp(mu[mod, k], var[mod, k]);
endp;

proc (1) = brei(y, x, mod);
    local t, u, v, m, br;
    
    t = rows(y);
    
    if mod == 0;
        v = x;
    elseif mod == 1;
        v = ones(t, 1)~x;
    elseif mod == 2;
        v = ones(t, 1)~seqa(1, 1, t)~x;
    endif;
    
    m = eye(t)-v*inv(v'v)*v';
    u = m*y;
    br = (t^(-1)*sumc(abs(cumsumc(u)).^2))/(sumc(u^2));
    
    retp(t^(-1)*br);
endp;



proc(2) = breimom(mod, k);
    local mu, var;
    
    mu = {  0.03084814	0.01946135	0.01263815	0.00885326	0.00647340	0.00488103	0.00374555 ,
        0.00847774	0.00655215	0.00515983	0.00416246	0.00332133	0.00272867	0.00227169 };
    
    var = { 0.00052376	0.00027834	0.00013093	0.00006222	0.00003198	0.00001718	0.00000905 ,
        0.00002145	0.00001480	0.00001031	0.00000714	0.00000435	0.00000297	0.00000193 };
    
    retp(mu[mod, k], var[mod, k]);
endp;
